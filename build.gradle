plugins {
    id 'java'
    id 'application'
    id 'com.github.johnrengelman.shadow' version '8.1.1'
    id 'com.bmuschko.docker-java-application' version '9.4.0'
}

group = 'com.github.dddpaul'
mainClassName = 'com.github.dddpaul.bpmnanalyzer.Application'

ext {
    dockerPrefix = "dddpaul"
}

repositories {
    mavenLocal()
    mavenCentral()
}

dependencies {
    implementation "io.camunda:zeebe-bpmn-model:8.3.4"
    implementation 'info.picocli:picocli:4.7.5'
    implementation 'org.slf4j:slf4j-simple:2.0.10'
    implementation 'com.google.code.gson:gson:2.10.1'
}

test {
    useJUnitPlatform()
}

import com.bmuschko.gradle.docker.tasks.image.Dockerfile
import com.bmuschko.gradle.docker.tasks.image.DockerBuildImage
import com.bmuschko.gradle.docker.tasks.image.DockerPushImage

task createDockerfile(type: Dockerfile) {
    dependsOn shadowJar
    destFile = project.file("${project.buildDir}/Dockerfile")
    from 'bellsoft/liberica-runtime-container:jre-21-slim-musl'
    label 'maintainer': 'Pavel Derendyaev "dddpaul@gmail.com"'
    addFile "libs/${project.name}-${project.version}-all.jar", '/app.jar'
    entryPoint 'java', '-Djava.security.egd=file:/dev/./urandom', '-jar', '/app.jar'
}

task buildDockerImage(type: DockerBuildImage) {
    dependsOn createDockerfile
    inputDir = project.file("${project.buildDir}")
    images.add("${dockerPrefix}/${project.name}:${project.version}")
}

task pushDockerImage(type: DockerPushImage) {
    dependsOn buildDockerImage
    images.add("${dockerPrefix}/${project.name}:${project.version}")
}
